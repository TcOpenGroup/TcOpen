<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="StartMotorsTask" Id="{816125d0-dfc6-0371-25e0-01209a3118e7}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK StartMotorsTask EXTENDS TcoMitsubishiRoboticsTask]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
    _blink(inOnTime := T#500MS, inOffTime := T#500MS);

    IF _progress = 0 THEN
        IF NOT __ISVALIDREF(inoControl) THEN
            _varInfo := __VARINFO(inoControl);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoConfig) THEN
            _varInfo := __VARINFO(inoConfig);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoStatus) THEN
            _varInfo := __VARINFO(inoStatus);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSE
            _progress := _progress + 1;
        END_IF
    END_IF

    IF _progress = 1 THEN
        inoControl.AutoEnable := TRUE;
        _varInfo := __VARINFO(inoStatus.State.AutoEnable);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.State.AutoEnable)
            .Warning(_sb.Clear().Append('<#Waiting for Auto signal! #>').Append(_varInfo.Symbol).ToString());

        IF inoStatus.State.AutoEnable THEN
            _progress := _progress + 1;
        END_IF;
    END_IF;

    IF _progress = 2 THEN
        inoControl.OperationEnable := TRUE;
        _varInfo := __VARINFO(inoStatus.State.OperationEnable);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.State.OperationEnable)
            .Warning(_sb.Clear().Append('<#Waiting for Operation enable signal! #>').Append(_varInfo.Symbol).ToString());

        IF inoStatus.State.OperationEnable THEN
            _progress := _progress + 1;
        END_IF;
    END_IF;

    IF _progress = 3 THEN
        Messenger.Debug('<#Robot Error Reset#>');
        inoControl.ErrorReset := inoStatus.State.ErrorReset AND _blink.outQ;

        IF NOT inoStatus.State.ErrorReset THEN
            inoControl.ErrorReset := FALSE;
            _progress := _progress + 1;
        END_IF
    END_IF;

    IF _progress = 4 THEN
        Messenger.Debug('<#Robot Emergency Stop Reset#>');
        inoControl.ErrorReset := inoStatus.State.EmergencyError AND _blink.outQ;

        IF NOT inoStatus.State.EmergencyError THEN
            inoControl.ErrorReset := FALSE;
            _progress := _progress + 1;
        END_IF
    END_IF;

    IF _progress = 5 THEN
        IF inoStatus.State.HighLevelError OR inoStatus.State.LowLevelError OR inoStatus.State.CautionLevelError OR inoStatus.State.EmergencyError
            THEN
            _progress := 1;
        END_IF

        _varInfo := __VARINFO(inoStatus.State.ServoOn);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.State.ServoOn)
            .Warning(_sb.Clear().Append('<# Waiting for signal: #>').Append(_varInfo.Symbol).ToString());

        _varInfo := __VARINFO(inoStatus.State.ErrorReset);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.State.ErrorReset)
            .Warning(_sb.Clear().Append('<# Waiting for signal: #>').Append(_varInfo.Symbol).ToString());

        inoControl.ServoOn := _blink.outQ;

        IF inoStatus.State.ServoOn AND NOT inoStatus.State.ErrorReset THEN
            inoControl.ServoOn := FALSE;
            _progress := _progress + 1;
        END_IF
    END_IF;

    DoneWhen(inoStatus.State.ServoOn AND NOT inoStatus.State.ErrorReset);
    CallTimers(TRUE);
    THIS^.ThrowWhen(_errorTimer.Q);
END_IF

IF THIS^.Error AND NOT _errorMessage.Pinned THEN
    THIS^.Restore();
END_IF]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{0ee0d3e1-9c70-089b-39d0-d9a036f4762a}">
      <Declaration><![CDATA[METHOD PUBLIC FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    ParentObject : ITcoObject;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{c2f2c5b7-a681-0ab6-3eb5-2702508aa7b5}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task is aborted.
        </summary>
    </docu>
~*)
METHOD PROTECTED OnAbort]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Start motors  was aborted.#>';
END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{f23c13f0-d705-0cfd-223c-f502185f98f6}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Done``` state .
        </summary>
    </docu>
~*)
METHOD PROTECTED OnDone]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Start motors  finished successfully.#>';
END_IF;

_progress := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{26a44469-395e-0339-2031-a917f721045f}">
      <Declaration><![CDATA[METHOD PROTECTED OnError
VAR
    description : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[description := '<#Start motors finished with error.#>';

IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := description;
END_IF;

THIS^._errorMessage.Error(_sb.Clear().Append(description).Append(' To restore acknowledge this alarm.').ToString()).Pin();]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{c2947b59-f44e-0229-32e0-4ecea6455ef9}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Start motors started.#>';
END_IF;

CallTimers(FALSE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{87943fdf-56c7-062f-2937-bed1ec3c0429}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
            <para>
                See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
            </para>
        </summary>
    </docu>
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>