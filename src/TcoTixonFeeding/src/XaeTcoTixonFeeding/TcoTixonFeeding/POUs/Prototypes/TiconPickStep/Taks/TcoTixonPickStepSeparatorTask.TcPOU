<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="TcoTixonPickStepSeparatorTask" Id="{ac76e352-eb60-01d0-2737-d7abcd936e64}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TcoTixonPickStepSeparatorTask EXTENDS TcoTixonFeedingTask
VAR_IN_OUT
    inoConfig : TcoTixonPickStep_Config_v_1_x_x;
    inoControl : TcoTixonPickStep_Control_v_1_x_x;
    inoStatus : TcoTixonPickStep_Status_v_1_x_x;
END_VAR
VAR
	_componentReady: BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
	 
  	IF _progress = 0 THEN
		IF NOT __ISVALIDREF(inoControl) THEN
			_varInfo :=__VARINFO(inoControl);
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSIF NOT __ISVALIDREF(inoConfig) THEN                                                  
			_varInfo :=__VARINFO(inoConfig);                                                    
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSIF NOT __ISVALIDREF(inoStatus) THEN                                                  
			_varInfo :=__VARINFO(inoStatus);                                                    
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSIF NOT inoStatus.ControllerInAuto THEN                                                  
			_varInfo :=__VARINFO(inoStatus.ControllerInAuto);                                                                                              
			Messenger.Warning(_sb.Clear().Append('<#Tixon control system not in auto #>').Append(_varInfo.Symbol).ToString());
		ELSE

			_progress := _progress + 1;

		END_IF

	END_IF
	IF _progress = 1 THEN
		inoControl.SepareComponent:=FALSE;
		_varInfo :=__VARINFO( inoStatus.SeparatorHomePos);                                                
		THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.SeparatorHomePos).Warning(_sb.Clear().Append('<#Waiting for #>').Append(_varInfo.Symbol).ToString());
		IF  inoStatus.SeparatorHomePos THEN
			_progress := _progress + 1;
			_timer(In:=FALSE);
		END_IF;
	END_IF;
	
	
	IF _progress = 2 THEN	
		Messenger.Debug('<#Waiting for component in  separator#>');
		_timer(IN:=inoStatus.SeparatorPresent,Pt:=inoConfig.TimeToComponentInSeparator);
		IF _timer.Q THEN
			_progress := _progress + 1;
			_timer(In:=FALSE);
		END_IF
	END_IF;
	
	IF _progress = 3 THEN	
		inoControl.SepareComponent:=TRUE;
		Messenger.Debug('<#Waiting for component out of  separator#>');
		_timer(IN:=inoStatus.SeparatorPresent,Pt:=inoConfig.TimeToComponentOutSeparator);
		IF inoStatus.SeparatorPresent THEN
			_progress := _progress + 1;
			_timer(In:=FALSE);
		ELSIF _timer.Q THEN
			_progress := 1;
			_timer(In:=FALSE);
		END_IF
	END_IF;
	
	IF _progress = 4 THEN	
		_componentReady:=TRUE;
		THIS^.DoneWhen(TRUE);
	END_IF;		

   
    CallTimers(TRUE);
	
	THIS^.ThrowWhen(_errorTimer.Q );
	
END_IF

inoStatus.ComponentReady:=_componentReady;

IF  THIS^.Error AND NOT _errorMessage.Pinned THEN
	THIS^.Restore();
END_IF]]></ST>
    </Implementation>
    <Property Name="ComponentReady" Id="{ed1f4a44-d784-0daa-3bc2-d6633a9627a9}">
      <Declaration><![CDATA[PROPERTY PUBLIC ComponentReady : bool]]></Declaration>
      <Get Name="Get" Id="{d65a8e30-4c52-0919-05cc-92b421a6e1b3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ComponentReady:=_componentReady;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnAbort" Id="{14e4fd0d-7e5f-0f46-09ad-ba1c3ee32096}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task is aborted.
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnAbort
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := '<#Separator task  was aborted.#>'; END_IF;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{7340e635-1e74-0fae-266f-0393008e2807}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Done``` state .
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnDone
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := '<#Separator task  finished successfully.#>'; END_IF;
_progress:=0;
IF AutoRestore THEN
	Restore();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{ab114dd6-4d9a-0bce-3f27-68e80c659893}">
      <Declaration><![CDATA[METHOD PROTECTED OnError
VAR
	description: STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[description := '<#Separator task finished with error.#>'; 
_componentReady:=false;
IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := description; END_IF;
THIS^._errorMessage.Error(_sb.Clear().Append(description).Append(' To restore acknowledge this alarm.').ToString()).Pin();]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{d75015c0-5a38-0c5d-369f-5619dd147cf7}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart 
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN  THIS^.inoStatus.ActionDescription := '<#Separator task started.#>'; END_IF; 
_componentReady:=false;
CallTimers(FALSE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{5f4f8652-2f9b-0082-3967-b05ac2c49250}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
			<para>
				See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
			</para>
		</summary>			
	</docu>	
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();
_componentReady:=false;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>