<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="TcoTixonPickVibroBasicSeparatorTask" Id="{9adc8cb8-6782-0569-1743-9e9447daa0e6}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TcoTixonPickVibroBasicSeparatorTask EXTENDS TcoTixonFeedingTask
VAR_IN_OUT
    inoConfig : TcoTixonPickVibroBasic_Config_v_1_x_x;
    inoControl : TcoTixonPickVibroBasic_Control_v_1_x_x;
    inoStatus : TcoTixonPickVibroBasic_Status_v_1_x_x;
END_VAR
VAR
    _componentReady : BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
    IF _progress = 0 THEN
        IF NOT __ISVALIDREF(inoControl) THEN
            _varInfo := __VARINFO(inoControl);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoConfig) THEN
            _varInfo := __VARINFO(inoConfig);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoStatus) THEN
            _varInfo := __VARINFO(inoStatus);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT inoStatus.ControllerInAuto THEN
            _varInfo := __VARINFO(inoStatus.ControllerInAuto);
            Messenger.Warning(_sb.Clear().Append('<#Tixon control system not in auto #>').Append(_varInfo.Symbol).ToString());
        ELSIF NOT inoConfig.SeparatorActive THEN
            _progress := 4;
        ELSE
            _progress := _progress + 1;
        END_IF
    END_IF

    IF _progress = 1 THEN
        inoControl.SepareComponent := FALSE;
        _varInfo := __VARINFO(inoStatus.SeparatorHomePos);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.SeparatorHomePos)
            .Warning(_sb.Clear().Append('<#Waiting for #>').Append(_varInfo.Symbol).ToString());

        IF inoStatus.SeparatorHomePos THEN
            _progress := _progress + 1;
            _timer(In := FALSE);
        END_IF;
    END_IF;

    IF _progress = 2 THEN
        Messenger.Debug('<#Waiting for component in  separator#>');
        _timer(IN := inoStatus.SeparatorPresent, Pt := inoConfig.TimeToComponentInSeparator);

        IF _timer.Q THEN
            _progress := _progress + 1;
            _timer(In := FALSE);
        END_IF
    END_IF;

    IF _progress = 3 THEN
        inoControl.SepareComponent := TRUE;
        _varInfo := __VARINFO(inoStatus.SeparatorWorkPos);

        THIS^.Messenger.OnCondition(_infoTimer.Q AND NOT inoStatus.SeparatorWorkPos)
            .Warning(_sb.Clear().Append('<#Waiting for #>').Append(_varInfo.Symbol).ToString());

        IF inoStatus.SeparatorWorkPos THEN
            _progress := _progress + 1;
            _timer(In := FALSE);
        END_IF;
    END_IF;

    IF _progress = 4 THEN
        Messenger.Debug('<#Waiting for component in Pick position#>');
        _timer(IN := inoStatus.SeparatorPresent, Pt := inoConfig.TimeToComponentOutSeparator);

        IF inoStatus.SeparatorPresent THEN
            _progress := _progress + 1;
            _timer(In := FALSE);
        END_IF
    END_IF;

    IF _progress = 5 THEN
        _componentReady := TRUE;
        THIS^.DoneWhen(TRUE);
    END_IF;

    CallTimers(TRUE);
    THIS^.ThrowWhen(_errorTimer.Q);
END_IF

inoStatus.ComponentReady := _componentReady;

IF THIS^.Error AND NOT _errorMessage.Pinned THEN
    THIS^.Restore();
END_IF]]></ST>
    </Implementation>
    <Property Name="ComponentReady" Id="{a73d0a30-c8a2-0105-1c0a-d842e7053b62}">
      <Declaration><![CDATA[PROPERTY PUBLIC ComponentReady : bool]]></Declaration>
      <Get Name="Get" Id="{030fb5aa-6396-0df9-378b-8102fdf409a9}">
        <Declaration><![CDATA[VAR
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[ComponentReady := _componentReady;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="OnAbort" Id="{0399a2fb-b74c-0858-1dbd-a2cca1677b3d}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task is aborted.
        </summary>
    </docu>
~*)
METHOD PROTECTED OnAbort]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Separator task  was aborted.#>';
END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{b7046509-f1cf-01ae-3e48-c3a893a5a687}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Done``` state .
        </summary>
    </docu>
~*)
METHOD PROTECTED OnDone]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Separator task  finished successfully.#>';
END_IF;

_progress := 0;

IF AutoRestore THEN
    Restore();
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{dab0b98d-e94c-0058-2b3f-0f511cd8250a}">
      <Declaration><![CDATA[METHOD PROTECTED OnError
VAR
    description : STRING;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[description := '<#Separator task finished with error.#>';
_componentReady := false;

IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := description;
END_IF;

THIS^._errorMessage.Error(_sb.Clear().Append(description).Append(' To restore acknowledge this alarm.').ToString()).Pin();]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{e8d220c7-9f50-09d0-2abf-05a4fdba1274}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF __ISVALIDREF(inoStatus) THEN
    THIS^.inoStatus.ActionDescription := '<#Separator task started.#>';
END_IF;

_componentReady := false;
CallTimers(FALSE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{04513d8f-db10-0035-300e-fe3f2bee4197}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
            <para>
                See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
            </para>
        </summary>
    </docu>
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();
_componentReady := false;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>