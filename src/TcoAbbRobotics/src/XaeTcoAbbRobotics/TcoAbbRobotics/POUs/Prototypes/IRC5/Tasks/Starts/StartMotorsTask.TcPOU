<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="StartMotorsTask" Id="{90007edc-04d7-0df9-1d24-a4d6e133c647}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK StartMotorsTask EXTENDS TcoCore.TcoTask 

VAR
    {attribute 'hide'} 
	_progress : INT := 0;
    {attribute 'hide'} 
	_infoTimer :  TON;
    {attribute 'hide'} 
	_errorTimer :  TON;
	{attribute 'hide'} 
	_timer :  TON;
    {attribute 'hide'} 
	_varInfo : __SYSTEM.VAR_INFO;
    {attribute 'hide'} 
	_sb : TcoCore.StringBuilder;
	_programRunning: BOOL;
	{attribute 'hide'}
	_blink:TcoUtilities.TcoBlinker(THIS^);
END_VAR

VAR_IN_OUT
    inoConfig 	: TcoIrc5_Config_v_1_x_x;
	inoControl 	: TcoIrc5_Control_v_1_x_x;
	inoStatus 	: TcoIrc5_Status_v_1_x_x;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[

IF Execute() THEN
	 _blink(inOnTime:=T#500MS,inOffTime:=T#500MS);

  	IF _progress = 0 THEN
		IF NOT __ISVALIDREF(inoControl) THEN
			_varInfo :=__VARINFO(inoControl);
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSIF NOT __ISVALIDREF(inoConfig) THEN                                                  
			_varInfo :=__VARINFO(inoConfig);                                                    
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSIF NOT __ISVALIDREF(inoStatus) THEN                                                  
			_varInfo :=__VARINFO(inoStatus);                                                    
			Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
		ELSE

			_progress := _progress + 1;

		END_IF

	END_IF
	IF _progress = 1 THEN
	
		_programRunning := FALSE;
		Messenger.Warning('<#Robot is not in Auto#>');
		IF  inoStatus.State.AutoOn THEN
			_progress := _progress + 1;
		END_IF;
	END_IF;
	
	
	IF _progress = 2 THEN	
		Messenger.Debug('<#Robot Error Reset#>');
		inoControl.ResetError := inoStatus.State.Error AND _blink.outQ;
		IF NOT inoStatus.State.Error THEN
			inoControl.ResetError:=FALSE;
			_progress := _progress + 1;
		END_IF
	END_IF;
	
	IF _progress = 3 THEN	
		Messenger.Debug('<#Robot Emergency Stop Reset#>');
		inoControl.ResetEmgStop := inoStatus.State.EmgStop AND _blink.outQ;
		IF NOT inoStatus.State.EmgStop THEN
			inoControl.ResetEmgStop:=FALSE;
			_progress := _progress + 1;
		END_IF
	END_IF;
		
	
	IF _progress = 4 THEN	
		IF inoStatus.State.Error THEN 
			_progress := 1;
		END_IF
		
		inoControl.MotorOn:=_blink.outQ;	
		IF inoStatus.State.MotorOnState  AND NOT inoStatus.State.Error THEN
			 inoControl.MotorOnAndStart:=FALSE;
			_progress := _progress + 1;
		END_IF
	END_IF;		
	
	
	
	
    DoneWhen(inoStatus.State.MotorOnState AND NOT inoStatus.State.Error);
    CallTimers(TRUE);
	
	THIS^.ThrowWhen(_errorTimer.Q);
	
END_IF]]></ST>
    </Implementation>
    <Method Name="CallTimers" Id="{00db6a13-3f64-09cb-15fb-4bd6afe9ebfa}">
      <Declaration><![CDATA[METHOD CallTimers
VAR_INPUT
	inValue : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoConfig) THEN  
_infoTimer(IN := inValue, PT := THIS^.inoConfig.InfoTime);
_errorTimer(IN := inValue, PT := THIS^.inoConfig.ErrorTime);
END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{063c6942-8f7a-054f-3536-8a61fe16b714}">
      <Declaration><![CDATA[METHOD PUBLIC FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    ParentObject : ITcoObject;	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{024284bb-c89c-08e3-2003-dbd51837f544}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task is aborted.
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnAbort
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := '<#Start motors  was aborted.#>'; END_IF;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{1d175901-a5df-08b2-35b6-5a962cc9d0c9}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Done``` state .
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnDone
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := '<#Start motors  finished successfully.#>'; END_IF;
_progress:=0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{421042f9-5277-0bc5-31f9-6fec70264897}">
      <Declaration><![CDATA[METHOD PROTECTED OnError
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN   THIS^.inoStatus.ActionDescription := '<#Start motors finished with error.#>'; END_IF;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{fcafa02c-1e47-0c37-102f-8c90b5ae05af}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart 
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF  __ISVALIDREF(inoStatus) THEN  THIS^.inoStatus.ActionDescription := '<#Start motors started.#>'; END_IF; 
CallTimers(FALSE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{57998319-88f1-0272-2abe-5f64266a6558}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
			<para>
				See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
			</para>
		</summary>			
	</docu>	
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[_timer(In:=FALSE);
SUPER^.Restore();
_progress := 0;
CallTimers(FALSE);
Messenger.UnPin();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>