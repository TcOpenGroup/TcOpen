<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="TcoInsightClearInspectionResults_v_5_x_x" Id="{497e7a72-a922-07c2-32a4-b0e893d03e9a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TcoInsightClearInspectionResults_v_5_x_x EXTENDS TcoCore.TcoTask
VAR_IN_OUT
    inoIoData : TcoInsightIO_v_5_x_x;
    inoConfig : TcoInsightConfig_v_5_x_x;
    inoStatus : TcoInsightStatus_v_5_x_x;
    inoResults : TcoInsightResults_v_5_x_x;
END_VAR
VAR
    {attribute 'hide'}
    _progress : INT := 0;
    {attribute 'hide'}
    _infoTimer : TON;
    {attribute 'hide'}
    _errorTimer : TON;
    {attribute 'hide'}
    _varInfo : __SYSTEM.VAR_INFO;
    {attribute 'hide'}
    _sb : TcoCore.StringBuilder;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
    IF _progress = 0 THEN
        IF NOT __ISVALIDREF(inoIoData) THEN
            _varInfo := __VARINFO(inoIoData);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoConfig) THEN
            _varInfo := __VARINFO(inoConfig);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoStatus) THEN
            _varInfo := __VARINFO(inoStatus);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSIF NOT __ISVALIDREF(inoResults) THEN
            _varInfo := __VARINFO(inoResults);
            Messenger.Programming(_sb.Clear().Append('<#Invalid reference: #>').Append(_varInfo.Symbol).ToString()).Pin();
        ELSE
            _progress := _progress + 1;
        END_IF
    END_IF

    IF _progress = 1 THEN
        THIS^.inoStatus.ActionDescription := '<#Clearing result data running.#>';
        CallTimers(FALSE);
        _progress := _progress + 1;
    END_IF

    IF _progress = 2 THEN
        THIS^.inoIoData.AcquisitionControl.AcquisitionTriggerEnable := FALSE;
        THIS^.inoIoData.AcquisitionControl.AcquisitionTrigger := FALSE;
        THIS^.inoIoData.InspectionControl.AcknowledgeInspectionResults := TRUE;
        THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the result data cleared!#>').Pin();
        THIS^.inoResults.InspectionId := 0;
        THIS^.inoResults.ResultCode := 0;

        IF Tc2_System.MEMSET(destAddr := ADR(THIS^.inoResults.InspectionResults), fillByte := 0, n := SIZEOF(THIS^.inoResults.InspectionResults)) > 0
            THEN
            CallTimers(FALSE);
            _progress := _progress + 1;
        END_IF
    END_IF

    IF _progress = 3 THEN
        THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal ResultsValid reseted!#>').Pin();

        IF NOT THIS^.inoIoData.InspectionStatus.ResultsValid THEN
            CallTimers(FALSE);
            _progress := _progress + 1;
        END_IF
    END_IF

    IF _progress = 4 THEN
        THIS^.inoIoData.InspectionControl.AcknowledgeInspectionResults := FALSE;
        THIS^.DoneWhen(TRUE);
        CallTimers(FALSE);
        _progress := 0;
    END_IF

    CallTimers(TRUE);
    THIS^.ThrowWhen(_errorTimer.Q);
END_IF]]></ST>
    </Implementation>
    <Method Name="CallTimers" Id="{ef0ea30e-8b34-0971-3864-b230ae22761c}">
      <Declaration><![CDATA[METHOD CallTimers
VAR_INPUT
    inValue : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_infoTimer(IN := inValue, PT := THIS^.inoConfig.InfoTime);
_errorTimer(IN := inValue, PT := THIS^.inoConfig.ErrorTime);]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{1ccc727f-9f23-0e13-2e2e-70aa9ab13dfe}">
      <Declaration><![CDATA[{attribute 'hide'}
METHOD PUBLIC FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    ParentObject : ITcoObject;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{b94d5c93-150f-00ad-2de0-04b8c2cfd226}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task is aborted.
        </summary>
    </docu>
~*)
METHOD PROTECTED OnAbort]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing of the inspection results aborted, while not yet finished.#>';]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{30d7f286-184f-053f-03ca-cc212d4e9b8d}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Done``` state .
        </summary>
    </docu>
~*)
METHOD PROTECTED OnDone]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing of the inspection results was completed succesfully.#>';]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{5a96b324-0bd6-00f5-24b3-25f63dfa1922}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Error``` state.
            <para>
                Do not confuse with <see cref="PlcDocu.TcoCore.TcoTask.WhileError()"/>, that is called cyclically until '''Error''' state is left.
            </para>
        </summary>
    </docu>
~*)
METHOD PROTECTED OnError]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing of the inspection results finished with error.#>';]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{5f716afa-fd95-0e33-09c1-58d6a6cd6787}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing of the inspection results started.#>';]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{d398922d-4573-09c0-11f0-e2626a22be02}">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
            <para>
                See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
            </para>
        </summary>
    </docu>
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();
_progress := 0;
CallTimers(FALSE);
Messenger.UnPin();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>