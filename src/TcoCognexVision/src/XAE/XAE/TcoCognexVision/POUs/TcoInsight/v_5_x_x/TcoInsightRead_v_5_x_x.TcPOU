<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="TcoInsightRead_v_5_x_x" Id="{e9941755-cc52-02d5-397d-31a9e59f08cb}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TcoInsightRead_v_5_x_x EXTENDS TcoCore.TcoTask
VAR_IN_OUT
	inoIoData : TcoDatamanIO_v_5_x_x;
    inoConfig : TcoDatamanConfig_v_5_x_x;
	inoStatus : TcoDatamanStatus_v_5_x_x;	
	inoResultData : TcoDatamanResults_v_5_x_x;
END_VAR
VAR
	_progress : INT := 0;
	_infoTimer :  TON;
	_errorTimer :  TON;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
	IF _progress = 0 THEN
		THIS^.inoStatus.ActionDescription := '<#Reading running.#>'; 
		CallTimers(FALSE);
		_progress := _progress + 1;
	END_IF
	
	IF _progress = 1 THEN
		THIS^.inoIoData.AcquisitionControl.TriggerEnable := TRUE;
		THIS^.inoIoData.AcquisitionControl.Trigger := FALSE;
		THIS^.inoIoData.ResultsControl.EnableResultBuffering := FALSE;
		THIS^.inoIoData.ResultsControl.ResultsAcknowledge := THIS^.inoIoData.ResultsStatus.ResultsAvailable;
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal ResultsAvailable reseted!#>'); 
		IF NOT THIS^.inoIoData.ResultsStatus.ResultsAvailable THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 2 THEN
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal TriggerReady set!#>'); 
		IF THIS^.inoIoData.AcquisitionStatus.TriggerReady THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 3 THEN
		THIS^.inoIoData.AcquisitionControl.Trigger := TRUE;
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal TriggerAcknowledge set!#>'); 
		IF THIS^.inoIoData.AcquisitionStatus.TriggerAcknowledge THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 4 THEN
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal ResultsAvailable set!#>'); 
		IF THIS^.inoIoData.ResultsStatus.ResultsAvailable THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 5 THEN
		THIS^.inoResultData.Length := THIS^.inoIoData.ResultData.ResultLength;
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the ResultData copied!#>'); 
		IF  Tc2_System.MEMCPY(srcAddr := ADR(THIS^.inoIoData.ResultData.ResultData) , destAddr:= ADR(THIS^.inoResultData.Data) ,  n:= THIS^.inoResultData.Length) > 0 AND 
			Tc2_System.MEMSET(destAddr:= ADR(THIS^.inoResultData.Data) + THIS^.inoResultData.Length, fillByte:= 0 , n:= SIZEOF(THIS^.inoResultData.Data) - THIS^.inoResultData.Length)>0 THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 6 THEN
		THIS^.inoIoData.AcquisitionControl.Trigger := FALSE;
		THIS^.inoIoData.ResultsControl.EnableResultBuffering := FALSE;
		THIS^.inoIoData.ResultsControl.ResultsAcknowledge := TRUE;
		THIS^.ThrowWhen(THIS^.inoIoData.ResultData.ResultCode=0 OR THIS^.inoIoData.ResultData.ResultLength=0);
		THIS^.DoneWhen(TRUE);
		_progress := 0;
	END_IF
	
	CallTimers(TRUE);
	
	THIS^.ThrowWhen(_errorTimer.Q);
	
END_IF
]]></ST>
    </Implementation>
    <Method Name="CallTimers" Id="{f372d964-5cdf-0719-0595-e1d95b8f7489}">
      <Declaration><![CDATA[METHOD CallTimers
VAR_INPUT
	inValue : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_infoTimer(IN := inValue, PT := THIS^.inoConfig.InfoTime);
_errorTimer(IN := inValue, PT := THIS^.inoConfig.ErrorTime);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{2b6eba60-dbce-01a8-0f19-e52b25a7b13e}">
      <Declaration><![CDATA[{attribute 'hide'}
METHOD PUBLIC FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	ParentObject : ITcoObject;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{949ac85c-96d0-0142-2317-d97a0fe47749}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task is aborted.
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnAbort
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Reading aborted, while not yet finished.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{4a50e4eb-7c12-046a-168f-26bc6d3c25d7}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Done``` state .
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnDone
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Reading done succesfull.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{b39c64c6-25cd-06e1-0be3-62a6e9c0b0e6}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Error``` state.
			<para>
				Do not confuse with <see cref="PlcDocu.TcoCore.TcoTask.WhileError()"/>, that is called cyclically until '''Error''' state is left.
			</para>
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnError
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Reading finished with error.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{3d34cf68-2a9b-02ff-0057-066f9b2a263e}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart 
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Reading started.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{97d7ee99-3a9a-03d7-23ba-3061a5e2e68c}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
			<para>
				See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
			</para>
		</summary>			
	</docu>	
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();
_progress := 0;
CallTimers(FALSE);

]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>