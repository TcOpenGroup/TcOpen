<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="TcoInsightClearResultData_v_5_x_x" Id="{fb9e5dea-a39e-0f70-2195-2dadceef360c}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK TcoInsightClearResultData_v_5_x_x EXTENDS TcoCore.TcoTask
VAR_IN_OUT
	inoIoData : TcoDatamanIO_v_5_x_x;
    inoConfig : TcoDatamanConfig_v_5_x_x;
	inoStatus : TcoDatamanStatus_v_5_x_x;	
	inoResultData : TcoDatamanResults_v_5_x_x;
END_VAR
VAR
	_progress : INT := 0;
	_infoTimer :  TON;
	_errorTimer :  TON;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF Execute() THEN
	IF _progress = 0 THEN
		THIS^.inoStatus.ActionDescription := '<#Clearing result data running.#>'; 
		CallTimers(FALSE);
		_progress := _progress + 1;
	END_IF
	
	IF _progress = 1 THEN
		THIS^.inoIoData.AcquisitionControl.TriggerEnable := FALSE;
		THIS^.inoIoData.AcquisitionControl.Trigger := FALSE;
		THIS^.inoIoData.ResultsControl.ResultsAcknowledge := TRUE;
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the result data cleared!#>');
		THIS^.inoResultData.Length := 0; 
		IF Tc2_System.MEMSET(destAddr:= ADR(THIS^.inoResultData.Data) , fillByte:= 0 , n:= SIZEOF(THIS^.inoResultData.Data)) >0 THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 2 THEN
		THIS^.Messenger.OnCondition(_infoTimer.Q).Warning('<#Waiting for the signal ResultsAvailable reseted!#>'); 
		IF NOT THIS^.inoIoData.ResultsStatus.ResultsAvailable THEN
			CallTimers(FALSE);
			_progress := _progress + 1;
		END_IF
	END_IF
	
	IF _progress = 3 THEN
		THIS^.inoIoData.ResultsControl.ResultsAcknowledge := FALSE;
		THIS^.DoneWhen(TRUE);
		CallTimers(FALSE);
		_progress := 0;
	END_IF
	
	CallTimers(TRUE);
	
	THIS^.ThrowWhen(_errorTimer.Q);
END_IF
]]></ST>
    </Implementation>
    <Method Name="CallTimers" Id="{9b3df9ae-1d58-04a1-0092-5aa89945cee9}">
      <Declaration><![CDATA[METHOD CallTimers
VAR_INPUT
	inValue : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[_infoTimer(IN := inValue, PT := THIS^.inoConfig.InfoTime);
_errorTimer(IN := inValue, PT := THIS^.inoConfig.ErrorTime);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{62833e51-9ee3-0ae6-1805-28427426501a}">
      <Declaration><![CDATA[{attribute 'hide'}
METHOD PUBLIC FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	ParentObject : ITcoObject;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{70e5ee0e-79d2-010d-2e5f-25535d4b92eb}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task is aborted.
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnAbort
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing result data aborted, while not yet finished.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{8ceafec6-c440-0334-3612-1d5032cd8ffc}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Done``` state .
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnDone
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing result data done succesfull.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{2ef9e334-22d1-0921-235e-44edfdcb5c66}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Executes when task reaches the ```Error``` state.
			<para>
				Do not confuse with <see cref="PlcDocu.TcoCore.TcoTask.WhileError()"/>, that is called cyclically until '''Error''' state is left.
			</para>
		</summary>			
	</docu>	
~*)
METHOD PROTECTED OnError
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing result data finished with error.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{35634dbf-296b-0cb3-2d8a-37ae3220759e}">
      <Declaration><![CDATA[METHOD PROTECTED OnStart 
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.inoStatus.ActionDescription := '<#Clearing result data started.#>'; 
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{5ddbb496-41a1-023f-2983-3a4f57e1c737}">
      <Declaration><![CDATA[(*~
	<docu>
		<summary>
			Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
			<para>
				See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
			</para>
		</summary>			
	</docu>	
~*)
METHOD Restore : ITcoRestorable]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Restore();
_progress := 0;
CallTimers(FALSE);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>